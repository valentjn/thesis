import copy
import fnmatch
import multiprocessing
import shlex
import os

Import("env")
Import("Helper")
env = env.Clone()


def buildSGpp(sgppModules):
  # set SCons flags for SG++
  sgppCppDir = Dir("sgpp")
  sgppSConsFlags = ["SG_ALL=0", "SG_PYTHON=1", "USE_PYTHON3_FOR_PYSGPP=1",
                    "USE_EIGEN=1", "USE_ARMADILLO=1", "USE_UMFPACK=1"]
  sgppArgs = (["scons", "-C", os.path.join("cpp", "sgpp"),
               "-j", str(multiprocessing.cpu_count())] + sgppSConsFlags +
              ["SG_{}=1".format(module.upper()) for module in sgppModules])

  sgppSourceFiles = []
  
  for module in sgppModules:
    moduleSourceDirs = [
      os.path.join(sgppCppDir.abspath, module, "build", "pysgpp"),
      os.path.join(sgppCppDir.abspath, module, "src"),
      os.path.join(sgppCppDir.abspath, module, "tests"),
    ]
    
    for moduleSourceDir in moduleSourceDirs:
      for root, dirs, files in os.walk(moduleSourceDir):
        for file_ in files:
          if os.path.splitext(file_)[1] in [".cpp", ".hpp", ".i"]:
            sgppSourceFiles.append(os.path.join(root, file_))
  
  sgppOutputFiles = [
    os.path.join("sgpp", "lib", "sgpp",
                 "libsgpp{}.so".format(module)) for module in sgppModules
  ] + [
    os.path.join("sgpp", "lib", "__init__.py"),
    os.path.join("sgpp", "lib", "pysgpp", "__init__.py"),
    os.path.join("sgpp", "lib", "pysgpp", "pysgpp_swig.py"),
    os.path.join("sgpp", "lib", "pysgpp", "_pysgpp_swig.so"),
  ]
  
  sgppCommandLine = " ".join([shlex.quote(arg) for arg in sgppArgs])
  sgppCommandLine += " && touch {}".format(" ".join(
      [shlex.quote(os.path.join("cpp", x)) for x in sgppOutputFiles]))
  env.Command(sgppOutputFiles, sgppSourceFiles, sgppCommandLine)
  sgppFiles = []
  
  for outputFile in sgppOutputFiles:
    installedFile = os.path.join(env["BUILD_DIR"].abspath, outputFile)
    env.InstallAs(installedFile, outputFile)
    sgppFiles.append(installedFile)
  
  return sgppCppDir, sgppFiles



def buildBiomech2Interface(compileOptions):
  biomech2InterfaceCppDir = Dir("biomech2_interface").abspath
  biomech2InterfaceBuildDir = os.path.join(
      env["BUILD_DIR"].abspath, "biomech2_interface")
  biomech2InterfaceCpps = []
  
  for root, dirs, files in os.walk(biomech2InterfaceCppDir):
    root = os.path.join(biomech2InterfaceBuildDir,
                        root[len(biomech2InterfaceCppDir):].lstrip("/"))
    biomech2InterfaceCpps.extend(
        [os.path.join(root, x) for x in fnmatch.filter(files, "*.cpp")])
  
  biomech2Interface = env.SharedLibrary(
      os.path.join(env["BUILD_DIR"].abspath, "libbiomech2_interface.so"),
      biomech2InterfaceCpps, **compileOptions)
  
  return biomech2Interface



def buildPrograms(sgppModules, sgppCppDir, sgppFiles):
  compileOptions = {
    "CPPPATH" : [os.path.join(sgppCppDir.abspath, module, "src")
                 for module in sgppModules] + [Dir(".").abspath],
    "CPPFLAGS" : ["-fopenmp", "-g", "-std=c++11", "-Wall", "-Wextra",
                  "-Wno-unused-parameter"],
    "LIBS" : ["sgpp{}".format(module) for module in sgppModules],
    "LIBPATH" : os.path.join(env["BUILD_DIR"].abspath, "sgpp", "lib", "sgpp"),
    "LINKFLAGS" : ["-fopenmp"],
  }
  
  env.VariantDir(env["BUILD_DIR"], ".", duplicate=False)
  
  biomech2Interface = buildBiomech2Interface(compileOptions)
  
  cpps = env.Glob(os.path.join(env["BUILD_DIR"].abspath, "*.cpp"))
  programs = []
  
  for cpp in cpps:
    name = os.path.splitext(os.path.basename(cpp.abspath))[0]
    program = os.path.join(env["BUILD_DIR"].abspath, name)
    
    curCompileOptions = copy.deepcopy(compileOptions)
    if name == "muscle": curCompileOptions["LIBS"].append(biomech2Interface)

    programs.append(env.Program(program, cpp, **curCompileOptions))
  
  env.Depends(programs, sgppFiles)
  
  return programs



# build SG++
sgppModules = ["base", "optimization"]
sgppCppDir, sgppFiles = buildSGpp(sgppModules)

# build programs
programs = buildPrograms(sgppModules, sgppCppDir, sgppFiles)

Return("sgppFiles", "programs")
