import os

Import("env")
env = env.Clone()

# remove temporary files created accidential run of LuaLaTeX instead of SCons
# (pressing the wrong button in TeXstudio)
for extension in ["aux", "bbl", "bcf", "blg", "lof", "log", "lot", "nlo", "out",
                  "pdf", "run.xml", "synctex.gz", "thm", "toc", "upa", "upb"]:
  path = "thesis.{}".format(extension)
  if os.path.isfile(path): os.remove(path)

# set output directory
env.Append(PDFLATEXFLAGS="--output-directory={}".format(env["BUILD_DIR"]))

# compile PDF
pdf = env.PDF(target="{}/thesis.pdf".format(env["BUILD_DIR"]), source="thesis.tex")

# execute Lua SConscript
lua = env.SConscript("../lua/SConscript", exports="env")
env.Depends(pdf, lua)

# copy hyphenation whitelist (needed in check mode)
hyphenation_whitelist = env.Install(env["BUILD_DIR"], "hyphenation_whitelist.txt")
env.Depends(pdf, hyphenation_whitelist)

Return("pdf")
