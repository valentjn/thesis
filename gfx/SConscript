import os
import subprocess

Import("env")
env = env.Clone()

# install pre-made graphics
pre = env.Glob("pre/*.pdf")
env.Install(env["BUILD_DIR"], pre)

# set output directory
env.Append(PDFLATEXFLAGS="--output-directory={}".format(env["BUILD_DIR"]))

# search for Python scripts
pys = env.Glob("py/*.py")
pdfs = []

# search for helper Python scripts
helper_pys = env.Glob("py/helper/*.py")

for py in pys:
  # determine number of generated images by script
  name = os.path.splitext(py.name)[0]
  
  with open(py.abspath, "r") as f: py_code = f.readlines()
  pdf_count = 1
  
  for line in py_code:
    if "number of output figures" in line.lower():
      number = line.split()[-1]
      if len(number.strip("0123456789")) == 0: pdf_count = int(number)
      break
  
  # generate list of *.pdf files
  pdfs = [os.path.join(env["BUILD_DIR"].abspath, "{}_{}.pdf".format(name, i))
          for i in range(1, pdf_count + 1)]
  # generate *.pdf files
  env.Command(pdfs, py, "BUILD_DIR=$BUILD_DIR python3 $SOURCE")
  # *.pdf files depend on helper scripts
  env.Depends(pdfs, helper_pys)

Return("pdfs")
