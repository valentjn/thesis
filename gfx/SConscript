import os
import subprocess
import sys

Import("env")
env = env.Clone()

# install pre-made graphics
pre = env.Glob("pre/*.pdf")
env.Install(env["BUILD_DIR"], pre)

# set output directory
env.Append(PDFLATEXFLAGS="--output-directory={}".format(env["BUILD_DIR"]))

# search for Python scripts
pys = env.Glob("py/*.py")
pdfs = []
pgfs = []
helper_pys = []

# home or Documents directory
home_dir = (os.environ["HOME"] if sys.platform.startswith("linux") else
            os.path.join(os.environ["HOMEDRIVE"], os.environ["HOMEPATH"], "Documents"))

# search for helper Python scripts
tools_dir = env.Dir(os.path.join(home_dir, "git", "prog", "python", "tools"))
tools_files = []
for root, dirs, files in os.walk(tools_dir.abspath):
  tools_files.extend([os.path.join(root, x) for x in files])

# set environment variables
sgpp_dir = os.path.join(home_dir, "sgpp", "sgopt")
py_env = env.Clone()
py_env["ENV"]["BUILD_DIR"]       = env["BUILD_DIR"].abspath
py_env["ENV"]["LD_LIBRARY_PATH"] = ":".join([py_env["ENV"].get("LD_LIBRARY_PATH", ""),
                                             os.path.join(sgpp_dir, "lib", "sgpp")])
py_env["ENV"]["PYTHONPATH"]      = ":".join([py_env["ENV"].get("PYTHONPATH", ""),
                                             tools_dir.abspath,
                                             os.path.join(sgpp_dir, "lib"),
                                             os.path.join(sgpp_dir, "lib", "pysgpp")])

for py in pys:
  # determine number of generated images by script
  name = os.path.splitext(py.name)[0]
  
  if name.startswith("helper_"):
    helper_pys.append(py)
    continue
  
  with open(py.abspath, "r") as f: py_code = f.readlines()
  pdf_count = 1
  
  for line in py_code:
    if "number of output figures" in line.lower():
      number = line.split()[-1]
      if len(number.strip("0123456789")) == 0: pdf_count = int(number)
      break
  
  # generate list of *.pdf files
  cur_pdfs = [os.path.join(env["BUILD_DIR"].abspath, "{}_{}.pdf".format(name, i))
              for i in range(1, pdf_count + 1)]
  # generate *.pdf files
  py_env.Command(cur_pdfs, py, "python3 $SOURCE")
  pdfs.extend(cur_pdfs)

  pgfs.extend([x[:-4] + ".pgf" for x in cur_pdfs])

# *.pdf files depend on helper scripts (local helper *.py files and global tools)
env.Depends(pdfs, [helper_pys, tools_files])

# don't delete PDFs before calling Python scripts
env.Precious(pdfs)

# clean *.xz data files as well
env.Clean(pdfs, [pgfs, env.Glob(os.path.join(env["BUILD_DIR"].abspath, "*.xz"))])

Return("pdfs")
