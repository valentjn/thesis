import os
import subprocess

def crop_pdf_action(target, source, env):
  for t in target:
    path = t.get_abspath()
    subprocess.call(["pdfcrop", path, path])

Import("env")
env = env.Clone()

# install pre-made graphics
pre = env.Glob("pre/*.pdf")
env.Install(env["BUILD_DIR"], pre)

# set output directory
env.Append(PDFLATEXFLAGS="--output-directory={}".format(env["BUILD_DIR"]))

# compile TeX graphics
texs = env.Glob("tex/*.tex")
pdfs = []

for tex in texs:
  pdf = "{}/{}.pdf".format(env["BUILD_DIR"], os.path.splitext(tex.name)[0])
  pdfs.append(env.PDF(target=pdf, source=tex))

env.AddPostAction(pdfs, crop_pdf_action)

Return("pdfs")
